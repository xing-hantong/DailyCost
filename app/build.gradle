apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion build_versions.compile_sdk
    defaultConfig {
        applicationId "me.hantong.dailycost"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 3
        versionName "1.0.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // 指定room.schemaLocation生成的文件路径
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        resConfigs "en"
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        debug {
            storeFile file("key/debug_key")
            storePassword "123456"
            keyAlias "key"
            keyPassword "123456"
        }
    }
    buildTypes {
        debug {
            // 签名
            signingConfig signingConfigs.debug
            versionNameSuffix ".debug"
            applicationIdSuffix ".debug"
            resValue "string", "scheme", "mk-debug"
        }

        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "DailyCost-${variant.baseName}-${variant.versionCode}-${variant.versionName}.apk"
            }
        }
    }
}

dependencies {
    uniformSupportVersion(configurations)
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // test
    testImplementation deps.junit
    androidTestImplementation deps.spresso_core
    // 支持库
    implementation deps.support.design
    // ViewModel and LiveData
    implementation deps.lifecycle.extensions
    annotationProcessor deps.lifecycle.compiler
    // Room
    implementation deps.room.runtime
    implementation deps.room.rxjava2
    annotationProcessor deps.room.compiler
    // 第三方库
    implementation deps.rxandroid
    // base adapter
    implementation deps.BRVAH
    // 路由
    implementation deps.floo
    // 图表
    implementation deps.MPChart
    // 动态权限申请
    implementation deps.permission
    // 保存文件
    implementation deps.storage
    // FirebaseUI
    implementation deps.firebase_ui_auth
    // Facebook Login
    implementation deps.facebook_login
    // ButterKnife
    implementation deps.jakewharton.butterknife
    annotationProcessor deps.jakewharton.butterknife_compiler

}
